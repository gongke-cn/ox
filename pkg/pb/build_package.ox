ref "std/io"
ref "std/dir"
ref "std/dir_r"
ref "std/path"
ref "std/shell"
ref "std/lang"
ref "json"
ref "./config"
ref "./tools"
ref "./log"

//Build the package.
public build_package: func(desc) {
    fn = "{config.outdir}/package.ox"

    if !need_update(fn, ["build.ox"]) {
        return
    }

    pkg = {
        name: desc.name
        description: desc.description
        version: desc.version
        architecture: get_arch(desc)
    }

    if !pkg.architecture {
        throw NullError(L"cannot get target architecture from the compiler")
    }

    if desc.homepage {
        pkg.homepage = desc.homepage
    }

    if desc.maintainer {
        pkg.maintainer = desc.maintainer
    }

    if desc.system_package {
        pkg.system_package = true
    }

    if desc.dependencies {
        pkg.dependencies = desc.dependencies
    }

    files = Set()

    //Add a file.
    add_file: func(src, dst) {
        if !Path(src).exist {
            throw NullError(L"\"{src}\" does not exist")
        }

        files.add(dst)
    }

    //Add a directory.
    add_dir: func(dn, dst) {
        #dir = Dir(dn)
        for dir as dent {
            if dent == "." || dent == ".." {
                continue
            }

            path = "{dn}/{dent}"
            fmt = Path(path).format
            if fmt == Path.FMT_DIR {
                add_dir(path, "{dst}/{dent}")
            } elif fmt == Path.FMT_REG {
                add_file(path, "{dst}/{dent}")
            }
        }
    }

    //Add a file or a directory.
    add_file_or_dir: func(src, dst) {
        st = Path(src)
        if !st.exist {
            throw NullError(L"\"{src}\" does not exist")
        }

        if st.format == Path.FMT_DIR {
            add_dir(src, dst)
        } elif st.format == Path.FMT_REG {
            files.add(dst)
        }
    }

    //Check if the module is an OXN.
    is_oxn: func(mod) {
        if desc.oxn_modules?[mod] {
            return true
        }

        if desc.oxngen_targets?[mod] {
            return true
        }

        return false
    }

    //Add a module to the files.
    add_mod: func(mod) {
        if is_oxn(mod) {
            src = "{config.outdir}/{mod}.oxn"
            dst = "%pkg%/{mod}.oxn"
        } else {
            src = "{mod}.ox"
            dst = "%pkg%/{mod}.ox"
        }

        add_file(src, dst)
    }

    //Libraries.
    if desc.libraries {
        pkg.libraries = desc.libraries
        for desc.libraries as lib {
            add_mod(lib)
        }
    }

    //Executable programs.
    if desc.executables {
        pkg.executables = desc.executables
        for desc.executables as exe {
            add_mod(exe)
        }
    }

    //Internal libraries.
    if desc.internal_libraries {
        for desc.internal_libraries as lib {
            add_mod(lib)
        }
    }

    //Internal files.
    if desc.internal_files {
        for desc.internal_files as file {
            add_file_or_dir(file, "%pkg%/{file}")
        }
    }

    //System files.
    if desc.system_files {
        for Object.entries(desc.system_files) as [dst, src] {
            if src.char_at(0) == '?' {
                src = src.slice(1)
                files.add(dst)
            } else {
                add_file_or_dir(src, dst)
            }
        }
    }

    //Add oxn files.
    if desc.oxn_modules {
        for Object.keys(desc.oxn_modules) as oxn {
            add_mod(oxn)
        }
    }

    //Add oxn files generated by oxngen.
    if desc.oxngen_targets {
        for Object.keys(desc.oxngen_targets) as oxn {
            add_mod(oxn)
        }
    }

    //Add mo files.
    lang_path = "{config.outdir}/locale"
    if Path(lang_path).exist {
        #lang_dir = Dir(lang_path)
        for lang_dir as dent {
            if dent == "." || dent == ".." {
                continue
            }

            dname = "{lang_path}/{dent}"
            if Path(dname).format != Path.FMT_DIR {
                continue
            }

            path = "{dname}/LC_MESSAGES/{desc.name}.mo"
            if Path(path).exist {
                add_file(path, "%locale%/{dent}/LC_MESSAGES/{desc.name}.mo")
            }
        }
    }

    //Add document.
    doc_path = "{config.outdir}/doc"
    if Path(doc_path).exist {
        #dir = DirR(doc_path, DirR.DIR_SKIP, "")
        for dir as ent {
            add_file("{doc_path}{ent}", "%doc%{ent}")
        }
    }

    files.add("%pkg%/package.ox")

    if files.length {
        pkg.files = files.$iter().to_array()
    }

    //Add pre/post events.
    if desc.pre_install {
        pkg.pre_install = desc.pre_install
    }

    if desc.post_install {
        pkg.post_install = desc.post_install
    }

    if desc.pre_uninstall {
        pkg.pre_uninstall = desc.pre_uninstall
    }

    if desc.post_uninstall {
        pkg.post_uninstall = desc.post_uninstall
    }

    File.store_text(fn, JSON.to_str(pkg, "  "))

    info(L"create {fn}")
}
