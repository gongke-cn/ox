ref "std/path"
ref "std/dir"
ref "std/dir_r"
ref "std/fs"
ref "oxp/oxp"
ref "json"
ref "./config"
ref "./log"
ref "./tools"

//Check if the module is an oxn.
is_oxn: func(desc, name) {
    if desc.oxn_modules && desc.oxn_modules[name] {
        return true
    }

    if desc.oxngen_targets && desc.oxngen_targets[name] {
        return true
    }

    return false
}

//Build the "oxp" package file.
public build_oxp: func(desc) {
    arch = get_arch(desc)

    oxp_name = "{config.outdir}/{desc.name}-{arch}-{desc.version}.oxp"

    #oxp = OxpWritter(oxp_name)
    set = Set()

    //Add a file.
    add_file: func(src, dst) {
        if set.has(dst) {
            return
        }

        set.add(dst)
        oxp.add_file(src, dst)
    }

    //Add a directory.
    add_dir: func(dn, dst) {
        #dir = Dir(dn)
        for dir as dent {
            if dent == "." || dent == ".." {
                continue
            }

            path = "{dn}/{dent}"
            fmt = Path(path).format
            if fmt == Path.FMT_DIR {
                add_dir(path, "{dst}/{dent}")
            } elif fmt == Path.FMT_REG {
                add_file(path, "{dst}/{dent}")
            }
        }
    }

    //Add a file or a directory.
    add_file_or_dir: func(src, dst) {
        st = Path(src)
        if !st.exist {
            throw NullError(L"\"{src}\" does not exist")
        }

        if st.format == Path.FMT_DIR {
            add_dir(src, dst)
        } elif st.format == Path.FMT_REG {
            add_file(src, dst)
        }
    }

    //package.ox
    add_file("{config.outdir}/package.ox", "package.ox")

    //Executable programs.
    if desc.executables {
        for desc.executables as exe {
            if !is_oxn(desc, exe) {
                add_file("{exe}.ox", "%pkg%/{exe}.ox")
            }
        }
    }

    //Libraries.
    if desc.libraries {
        for desc.libraries as lib {
            if !is_oxn(desc, lib) {
                add_file("{lib}.ox", "%pkg%/{lib}.ox")
            }
        }
    }

    //Internal libraries.
    if desc.internal_libraries {
        for desc.internal_libraries as lib {
            if !is_oxn(desc, lib) {
                add_file("{lib}.ox", "%pkg%/{lib}.ox")
            }
        }
    }

    //OXN modules.
    if desc.oxn_modules {
        for Object.keys(desc.oxn_modules) as oxn {
            add_file("{config.outdir}/{oxn}.oxn", "%pkg%/{oxn}.oxn")
        }
    }

    //OXN modules generated by oxngen.
    if desc.oxngen_targets {
        for Object.keys(desc.oxngen_targets) as oxn {
            add_file("{config.outdir}/{oxn}.oxn", "%pkg%/{oxn}.oxn")
        }
    }

    //Internal files.
    if desc.internal_files {
        for desc.internal_files as file {
            add_file_or_dir(file, "%pkg%/{file}")
        }
    }

    //System files.
    if desc.system_files {
        for Object.entries(desc.system_files) as [dst, src] {
            if src.char_at(0) == '?' {
                src = src.slice(1)

                set.add(dst)
                oxp.add_symlink(src, dst)
            } else {
                add_file_or_dir(src, dst)
            }
        }
    }

    //MO files.
    lang_path = "{config.outdir}/locale"
    if Path(lang_path).exist {
        #lang_dir = Dir(lang_path)
        for lang_dir as dent {
            if dent == "." || dent == ".." {
                continue
            }

            dname = "{lang_path}/{dent}"
            if Path(dname).format != Path.FMT_DIR {
                continue
            }

            path = "{dname}/LC_MESSAGES/{desc.name}.mo"
            if Path(path).exist {
                add_file(path, "%locale%/{dent}/LC_MESSAGES/{desc.name}.mo")
            }
        }
    }

    //Document.
    doc_path = "{config.outdir}/doc"
    if Path(doc_path).exist {
        #dir = DirR(doc_path, DirR.DIR_SKIP, "")
        for dir as ent {
            add_file("{doc_path}{ent}", "%doc%{ent}")
        }
    }

    info(L"create {oxp_name}")
}
