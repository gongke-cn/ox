# 变量
## 变量声明
OX语言中每一个变量都以一个标识符作为其名称。OX语言通过不同的标识符引用不同的变量。
一个标识符作为一个语句，代表声明一个以此标识符为名称的变量。如：
```
a //声明变量a
```
新声明的变量初始值为null。

如果需要在声明变量的同时指定变量的值，可以使用一个给标识符赋值的语句：
```
a = 1 //声明变量a，并设定其值为数字1
```
在声明变量之后，就可以通过标识符引用这个变量，或者重新将变量设置为一个新的值：
```
a = 1 //声明变量a，并设定其值为数字1

stdout.puts("value: {a}\n") //打印变量a的值，输出 "value: 1\n"

a = 2 //重新设定变量a的值为2

stdout.puts("value: {a}\n") //重新打印变量a的值, 输出 "value: 2\n"
```
注意如果在给变量赋值语句前引用变量，变量的值为其初始值null。
```
stdout.puts("value: {v}\n") //v没有赋值，其值为null, 输出 "value: \n"
v = 1
stdout.puts("value: {v}\n") //输出 "value: 1\n"
```
有时我们希望声明的标识符的值不会被修改，我们可以用以下语句声明一个常量：
```
c: 3721 //声明常量c，将其值设定为数字3721

c = 0 //错误：常量c不能修改
```

语法描述：
```
variable_declaration: identifier ("=" expression)?

constant_declaration: identifier ":" expression
```
## 变量的作用域
一个变量的作用域在其所属的函数内部，看以下的代码：
```
my_func: func() {
    v = 1 //声明变量v, 作用域在my_func函数中
}

stdout.puts(v) //错误：变量v不在此作用域中
```
在函数my_func中声明的变量v,只在函数my_func内部有效，函数外部是无法引用此变量的。

在OX语言中，函数是可以嵌套的，在内部的函数是可以引用外部函数声明的变量。看以下代码：
```
outter: func() {
    v = 1 //声明变量v

    inner: func() {
        stdout.puts(v) //内部函数可以访问外部函数声明的变量
    }

    inner() //调用内部函数，打印变量v的值
}
```
外部函数声明了变量v, 内部变量可以引用访问此变量。

注意内部函数和外部函数是可以声明同名变量的。看以下代码：
```
outter: func() {
    v = 1 //声明变量v

    inner: func() {
        v = 2 //内部函数也声明变量v

        stdout.puts("{v}\n") //打印内部函数定义的v值"2"
    }

    inner() //调用内部函数
    stdout.puts("{v}\n") //打印外部函数定义的v值"1"
}
```
一个函数中如果使用"标识符语句"或“标识符赋值语句”，都会认为以此标识符在当前函数域中声明一个新的变量。
当程序中用一个标识符引用变量时，OX语言会优先在当前函数域中查找变量，没有找到对应变量时，才会依次查找外层函数中的变量定义。

如果在内层函数中想对外层函数中定义的变量赋值，而不是声明一个新的变量，可以在变量标识符前加"@"标识其是一个外层变量。
```
outter: func() {
    v = 1 //声明变量v

    inner: func() {
        @v = 2 //设定外部变量v
    }

    inner()
    stdout.puts("{v}\n") //v值已被修改，打印"2"
}
```
如果内部函数需要多次引用一个外部变量，只需要一处添加"@"将其标识为外部变量。如：
```
outter: func() {
    v = 1 //声明变量v

    inner: func() {
        @v = 2 //设定外部变量v
        v = 3 //设定外部变量v
    }

    inner()
    stdout.puts("{v}\n") //v值已被修改，打印"3"
}
```

