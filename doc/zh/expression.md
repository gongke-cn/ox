# 表达式
语法描述：
```
expression: variable_declaration
    | constant_declaration
    | null_literal
    | bool_literal
    | number_literal
    | character_literal
    | string_literal
    | array_literal
    | array_append_items
    | object_literal
    | object_append_properties
    | parentheses_expression
    | get_property_expression
    | get_item_expression
    | function_call_expression
    | unary_expression
    | math_expression
    | shift_expression
    | bitwise_expression
    | instof_expression
    | match_expression
    | compare_expression
    | equal_expression
    | logic_and_expression
    | logic_or_expression
    | assignment_expression
    | reverse_assignment_expression
    | self_operation_assignment_expression
    | regular_expression
    | if_statement
    | case_statement
    | function_declaration
    | class_declaration
    | yield_expression
    | get_pointer_expression
    | get_value_expression
```
## 操作符优先级
|优先级|操作符|说明|方向|
|:-|:-|:-|:-|
|1|()|小括号|从左到右|
|2|.|访问属性|从左到右|
|2|[]|访问元素|从左到右|
|2|()|调用函数|从左到右|
|3|-|负号|从右到左|
|3|+|正号|从右到左|
|3|!|逻辑非运算|从右到左|
|3|~|按位取反运算|从右到左|
|3|*|C指针取值操作|从右到左|
|3|&|C值取指针操作|从右到左|
|3|typeof|取对象类|从右到左|
|4|**|幂运算|从左到右|
|5|*|乘法运算|从左到右|
|5|/|除法运算|从左到右|
|5|%|取模运算|从左到右|
|6|+|加法运算|从左到右|
|6|-|减法运算|从左到右|
|7|<<|左移位运算|从左到右|
|7|>>|右移位运算|从左到右|
|7|>>>|无符号右移位运算|从左到右|
|8|<|小于|从左到右|
|8|<=|小于或等于|从左到右|
|8|>|大于|从左到右|
|8|>=|大于或等于|从左到右|
|8|<|小于|从左到右|
|8|instof|实例检测|从左到右|
|9|==|等于|从左到右|
|9|!=|不等于|从左到右|
|10|&|按位与|从左到右|
|11|^|按位异或|从左到右|
|12|\||按位或|从左到右|
|13|&&|逻辑与|从左到右|
|14|\|\||逻辑或|从左到右|
|15|=|赋值|从右到左|
|15|+=|自加后赋值|从右到左|
|15|-=|自减后赋值|从右到左|
|15|*=|自乘后赋值|从右到左|
|15|/=|自除后赋值|从右到左|
|15|%=|自取模后赋值|从右到左|
|15|**=|自求幂后赋值|从右到左|
|15|<<=|左移位后赋值|从右到左|
|15|>>=|右移位后赋值|从右到左|
|15|>>>=|无符号右移位后赋值|从右到左|
|15|&=|按位与后赋值|从右到左|
|15|\|=|按位或后赋值|从右到左|
|15|^=|按位异或后赋值|从右到左|
|15|&&=|为真情况下赋值|从右到左|
|15|\|\|=|为假情况下赋值|从右到左|

## 类型转化操作
在一些运算过程中，OX会自动对操作数进行类型转换。常用的类型转换包括以下几种。
### 转化为布尔值
|原始类型|结果|
|:-|:-|
|null|false|
|布尔值|返回原始值|
|数值|数值为0结果为false，其他值结果为true|
|字符串|长度为0结果为false,其他值结果为true|
|对象|true|

### 转化为数值
|原始类型|结果|
|:-|:-|
|null|0|
|布尔值|true结果为1, false结果为0|
|数值|返回原始值|
|字符串|按数字型字面量表示方式进行解析，返回结果数值。如果字符串不能解析为数值，返回Number.NAN|
|对象|调用对象的"$to_num()"方法返回结果数值|

### 转化为32位无符号整数
具体操作如下:

* 将操作数转化为数值
* 如果数值小于0或大于4294967295, 抛出RangeError错误
* 截取整数部分变为整数

### 转化为32位有符号整数
具体操作如下:

* 将操作数转化为数值
* 如果数值小于-2147483648或大于2147483647, 抛出RangeError错误
* 截取整数部分变为整数

### 转化为64位无符号整数
具体操作如下:

* 如果操作数为64位整数，直接将其转化为无符号64位整数返回。
* 将操作数转化为数值
* 如果数值小于0或大于18446744073709551615, 抛出RangeError错误
* 截取整数部分变为整数

### 转化为64位有符号整数
具体操作如下:

* 如果操作数为64位整数，直接将其转化为有符号64位整数返回。
* 将操作数转化为数值
* 如果数值小于-9223372036854775808或大于9223372036854775807, 抛出RangeError错误
* 截取整数部分变为整数

### 转化为字符串
|原始类型|结果|
|:-|:-|
|null|""|
|布尔值|true结果为"true", false结果为"false"|
|数值|按格式"n"将数值转化为字符串|
|字符串|返回原始值|
|对象|调用对象的"$to_str()"方法返回结果数值|

## 小括号
小括号在所有运算中有最高优先级。
```
1 + 2 * 3 //乘法优先级高于加法，结果为7
(1 + 2) * 3 //小括号内优先级最高，结果为9
```

语法描述：
```
parentheses_expression: "(" expression ")"
```
## 访问属性
访问一个对象的属性。
```
o.prop //访问对象o的属性"prop"
"string".length //访问字符串"string"的属性"length"
a.b.c //获取对象a的属性"b"对象,并访问这个对象的属性"c"
```
"."符号前为对象表达式，"."符号后为一个标识符代表属性的名称。

语法描述：
```
get_property_expression: expression "." identifier
```
## 访问元素
访问一个数组的元素或一个对象的属性。
```
a[0] //访问数组a的元素0
o["prop"] //访问对象o的属性"prop"
```
中括号前为要访问的数组或对象，中括号内为一个有效的表达式。如果表达式类型为数值，表示访问元素的索引。如果表达式类型为字符串，表示访问属性的名称。

语法描述：
```
get_item_expression: expression "[" expression "]"
```
## 函数调用
调用一个函数。
```
my_func() //调用函数"my_func"
my_func_1(0) //调用函数“my_func_1”, 并传入一个参数
my_func_2(0, 1) //调用函数“my_func_2”, 并传入两个参数
my_func_3(0, 1, my_func_4()) //调用函数“my_func_3”, 并传入三个参数，其中第三个参数为调用函数“my_func_4”的返回值
```
如果我们想将一个数组的多个元素作为参数传入函数，可以使用"..."将数组元素展开：
```
a = [1,2,3]
my_func(...a) //等价于my_func(1,2,3)
a1 = [1,2,3]
a2 = [4,5,6]
my_func(...a1, ...a2) //等价于my_func(1,2,3,4,5,6)
```

语法描述：
```
function_call_expression: expression "(" arguments? ")"

arguments: arguments "," argument
    | argument

argument: expression
    | "..." expression
```
## 单目运算
单目运算在操作符右侧带一个操作数。

语法描述：
```
unary_expression: unary_operator expression

unary_operator: "+"
    | "-"
    | "!"
    | "~"
    | "typeof"
```
### +
运算具体操作如下：

* 如果操作数为64位整数
    - 返回64位整数
* 如果操作数为其他类型
    - 将操作数转化为数值
    - 返回数值

```
+123
+1.234
+true //结果为1
+“0” //结果为0
```
### -
运算具体操作如下：

* 如果操作数为64位整数
    - 将操作数转化为64位有符号整数
    - 对64位有符号整数取负数
    - 返回结果64位有符号整数
* 如果操作数为其他类型
    - 将操作数转化为数值
    - 对数值取负数
    - 返回结果数值

```
-1
-0.123
-"-3.14" //结果为3.14
```
### 逻辑非
运算具体操作如下：

* 将操作数转化为布尔值
* 对布尔值取逻辑非值
* 返回结果布尔值

```
!true //结果为false
!false //结果为true
!0 //结果为true
```
### 按位取反
运算具体操作如下：

* 如果操作数是64位整数
    - 将操作数转化为64位无符号整数
    - 对64位无符号整数进行按位取反操作
    - 返回结果64位无符号整数
* 如果操作数不是64位整数
    - 将其转化为32位无符号整数
    - 对32位无符号整数进行按位取反操作
    - 将结果转化为数值
    - 返回结果数值

```
~0 //结果为0xffffffff
~1 //结果为0xfffffffe
```
### typeof
返回操作数所属的类：

|操作数类型|typeof结果|
|:-|:-|
|null|null|
|布尔型|Bool|
|数值型|Number|
|字符串|String|
|对象|对象所属的类|
|C值|C值对应的C类型|
|其他|null|

## 数学运算
数学运算为双目运算。

语法描述：
```
math_expression: expression math_operator expression

math_operator: "+"
    | "-"
    | "*"
    | "/"
    | "%"
    | "**"
```
### 加法
运算具体操作如下：

* 如果两个操作数中有一个为字符串
    - 将两个操作数转化为字符串
    - 进行字符串连接操作
    - 返回结果字符串
* 如果两个操作数中有一个为64位整数
    - 如果两个操作数中有一个为64位无符号整数
        * 将两个操作数转化为64位无符号整数
        * 进行64位整数加法运算
        * 返回结果64位无符号整数
    - 如果两个操作数都不是64位无符号整数
        * 将两个操作数转化为64位有符号整数
        * 进行64位整数加法运算
        * 返回结果64位有符号整数
* 其他操作数类型
    - 将两个操作数转化为数值
    - 进行加法运算
    - 返回结果数值

```
1 + 2 //结果为3
"a" + "b" //结果为"ab"
```
### 减法/乘法/除法/取模/指数运算

|操作符|描述|
|:-|:-|
|-|减法|
|*|乘法|
|/|除法|
|%|取模|
|**|指数运算|

运算具体操作如下：

* 如果两个操作数中有一个为64位整数
    - 如果两个操作数中有一个为64位无符号整数
        * 将两个操作数转化为64位无符号整数
        * 进行64位无符号数数学运算
        * 返回结果64位无符号整数
    - 如果两个操作数都不是64位无符号整数
        * 将两个操作数转化为64位有符号整数
        * 进行64位有符号整数数学运算
        * 返回结果64位有符号整数
* 其他操作数类型
    - 将两个操作数转化为数值
    - 进行数学运算
    - 返回结果数值

```
1 - 2 //结果为-1
2 * 3 //结果为6
3 / 2 //结果为1.5
3 % 2 //结果为1
```

## 移位运算

|操作符|描述|
|:-|:-|
|<<|向左移位|
|>>|向右移位(最高位不参与移位)|
|>>>|向右移位(最高位参与移位)|

运算具体操作如下：

* 如果左操作数为64位整数
    - 如果是无符号右移位操作(>>>)操作
        * 将左操作数转化为64位无符号数
        * 将左操作数转化为32位无符号数
        * 进行无符号右移位操作
        * 返回结果64位无符号数
    - 如果不是无符号右移位操作(>>>)操作
        * 将左操作数转化为64位有符号数
        * 将左操作数转化为32位无符号数
        * 进行有符号移位操作(做右移位时最高位视为符号位，不移动)
        * 返回结果64位有符号数
* 其他操作数类型
    - 如果是无符号右移位操作(>>>)操作
        * 将左操作数转化为32位无符号数
        * 将左操作数转化为32位无符号数
        * 进行无符号右移位操作
        * 返回结果32位无符号数
    - 如果不是无符号右移位操作(>>>)操作
        * 将左操作数转化为32位有符号数
        * 将左操作数转化为32位无符号数
        * 进行有符号移位操作(做右移位时最高位视为符号位，不移动)
        * 返回结果32位有符号数

```
1 << 1 //左移位，结果为2
2 >> 1 //右移位，结果为1
-2 >> 1 //右移位，结果为-1
-2 >>> 1 //右移位，结果为0x7fffffff
```

语法描述：
```
shift_expression: expression shift_operator expression

shift_operator: "<<"
    | ">>"
    | ">>>"
```
## 位运算

|操作符|描述|
|:-|:-|
|\||按位或运算|
|&|按位与运算|
|^|按位异或运算|

运算具体操作如下：

* 如果两个操作数中有一个为64位整数
    - 如果两个操作数中有一个为64位无符号整数
        * 将两个操作数转化为64位无符号整数
        * 进行64位整数位运算
        * 返回结果64位无符号整数
    - 如果两个操作数都不是64位无符号整数
        * 将两个操作数转化为64位有符号整数
        * 进行64位整数位运算
        * 返回结果64位有符号整数
* 其他操作数类型
    - 将两个操作数转化为32位无符号数
    - 进行位运算
    - 将结果32位无符号数转化为数值
    - 返回数值

```
0b11110000 | 0b00001111 //按位或，结果为0b1111_1111
0b11111111 & 0b11110000 //按位与，结果为0b1111_0000
0b11110000 ^ 0b11000011 //按位异或，结果为0b0011_0011
```

语法描述：
```
bitwise_expression: expression bitwise_operator expression

bitwise_operator: "|"
    | "&"
    | "^"
```

## instof
检查左操作数是否为右操作数的一个实例。
右操作数一般为一个类，如果左操作数是这个类的一个实例，操作返回true，否则返回false。
```
true instof Bool //返回true
1 instof Bool //返回false
"str" instof String //返回true
```

语法描述：
```
instof_expression: expression "instof" expression
```
## 匹配操作
对字符串用正则表达式或字符串进行匹配。如果匹配成功，返回匹配字符串。否则返回null。
```
"abc123" ~ /[a-z]+/ //返回"abc"
"abc123" ~ "123" //返回"123"
"abc123" ~ "abcd" //返回null
```

语法描述：
```
match_expression: expression "~" expression
```
## 比较运算

|操作符|描述|
|:-|:-|
|<|小于|
|>|大于|
|<=|小于或等于|
|>=|大于或等于|

运算具体操作如下：

* 如果两个操作数中有一个为字符串
    - 将两个操作数转化为字符串
    - 进行字符串比较操作
    - 返回布尔值表示比较结果
* 如果两个操作数中有一个为64位整数
    - 如果两个操作数中有一个为64位无符号整数
        * 将两个操作数转化为64位无符号整数
        * 比较两个64位无符号整数
        * 返回布尔值表示比较结果
    - 如果两个操作数都不是64位无符号整数
        * 将两个操作数转化为64位有符号整数
        * 比较两个64位有符号整数
        * 返回布尔值表示比较结果
* 其他操作数类型
    - 将两个操作数转化为数值
    - 比较两个数值
    - 返回布尔值表示比较结果

```
1 < 0 //结果为false
1 > 0 //结果为true
“a” < "b" //结果为true
```
字符串比较操作如下：

* 从索引0开始依次比较两个字符串中的字符
* 如果字符相同，则比较下一个字符
* 如果左操作数字符的编码值小于右操作数字符的编码值，则结束比较过程，结果为左字符串小于右字符串。
* 如果左操作数字符的编码值大于右操作数字符的编码值，则结束比较过程，结果为左字符串大于右字符串。
* 如果两字符串前面的字符都相同，而右字符串长比左字符串长，则结果为左字符串小于右字符串。
* 如果两字符串前面的字符都相同，而右字符串长比左字符串短，则结果为左字符串大于右字符串。
* 如果两字符串前面的字符都相同，而左右字符串的长度也相同，则结果为两字符串相等。

当需要对一个表达式做多次比较操作时:
```
0 < a && a < 10
```
可以简化为：
```
0 < a < 10
```

语法描述：
```
compare_expression: expression compare_operator expression

compare_operator: "<"
    | ">"
    | "<="
    | ">="
```

## 等于/不等运算

|操作符|描述|
|:-|:-|
|==|等于|
|!=|不等于|

比较两个操作数是否相等。

* 如果两个操作数中都是字符串
    - 进行字符串比较
    - 返回布尔值表示比较结果
* 如果两个操作数中都是数值或C数值
    - 将两个数值转化为相同类型
    - 进行数值比较
    - 返回布尔值表示比较结果
* 操作数为其他类型
    - 如果两个操作数的类型和值都一样，则表示两个操作数相等，否则为不相等。
    - 返回布尔值表示比较结果

```
1 == 1 //结果为true
1 != 0 //结果为true
0 == null //结果为false
```
注意相等不相等比较时不会自动转化操作数类型，因此"0 == null"结果为false。

语法描述：
```
equal_expression: expression equal_operator expression

equal_operator: "=="
    | "!="
```

## 逻辑与运算
运算具体操作如下：

* 将左操作数转化为布尔值，如果结果为false，直接返回左操作数的原始值。
* 返回右操作数的值。

```
1 && 2 //结果为2
1 && 0 //结果为0
0 && 2 //结果为0
```
注意如果左操作数转化为布尔值结果为假，右操作数不会被执行。
```
false && run() //run()函数不会被调用。
```

语法描述：
```
logic_and_expression: expression "&&" expression
```
## 逻辑或运算
运算具体操作如下：

* 将左操作数转化为布尔值，如果结果为true，直接返回左操作数的原始值。
* 返回右操作数的值。

```
1 || 2 //结果为1
0 || 1 //结果为1
0 || 2 //结果为2
```
注意如果左操作数转化为布尔值结果为真，右操作数不会被执行。
```
true && run() //run()函数不会被调用。
```

语法描述：
```
logic_or_expression: expression "||" expression
```
## 赋值
将一个表达式的计算结果赋值给一个左值表达式。
注意只有左值表达式可以被赋值。赋值给一个非左值表达式编译器会报错。
左值表达式可以是以下几种:

|类型|示例|
|:-|:-|
|变量|v|
|属性|o.prop|
|元素|a[0]|
|指针取值|*ptr|

请看以下赋值语句：
```
v = 1 //将变量v赋值为1
o.prop = true //将对象o的属性"prop"赋值为true
a[1] = "item 1" //将数组a的元素1赋值为字符串"item 1"
*ptr = 1 //设置指针指向的C值为1
```
下面的赋值语句是错误的：
```
c: 1 //声明常量c
c = 2 //赋值错误：不能给常量赋值。
100 = 1 //赋值错误：100不是一个左值表达式。
a + b = true //赋值错误：a + b 不是一个左值表达式。
```

语法描述：
```
assignment_expression: assignment_left_value "=" expression

assignment_left_value: identifier
    | get_property_expression
    | get_item_expression
    | get_value_expression
```
## 反向赋值
OX语言支持反向赋值，形式如下：
```
1 => a
```
反向赋值使用操作符"=>",操作符右侧为赋值目标的左值表达式，操作符左侧为要设的值。
除了一般的赋值操作，反向赋值语句还支持一条语句赋值给多个目标。比如下面的语句：
```
a => [v1, v2, v3]
```
表示将数组a的元素0, 1, 2的值分别赋值给变量v1, v2, v3。此操作等价于:
```
v1 = a[0]
v2 = a[1]
v3 = a[2]
```
反向赋值还支持按对象属性名称匹配的方式赋值多个目标。比如下面的语句：
```
o => {p1:v1, p2:v2, p3:v3}
```
此操作等价于：
```
v1 = o.p1
v2 = o.p2
v3 = o.p3
```
如果目标变量名和属性名称相同，赋值语句还可以简化为:
```
o => {p1, p2, p3}
```
此操作等价于:
```
p1 = o.p1
p2 = o.p2
p3 = o.p3
```

语法描述：
```
reverse_assignment_expression: expression "=>" reverse_assginment_left_value

reverse_assginment_left_value: assginment_left_value
    | array_assignment_left_value
    | object_assignment_left_value

array_assignment_left_value: "[" array_assignment_left_items? "]"

array_assignment_left_items: array_assignment_left_items "," array_assignment_left_item
    | array_assignment_left_item

array_assignment_left_item: reverse_assginment_left_value

object_assignment_left_value: "{" object_assignment_left_items? "}"

object_assignment_left_items: object_assignment_left_items "," object_assignment_left_item
    | object_assignment_left_item

object_assignment_left_item: property_name ":" reverse_assginment_left_value
    | identifier
```
## 自操作赋值
自操作即先对左值表达式进行取值，然后对结果进行计算，再将最后的计算结果赋值给左值表达式。
```
a += 1 //等价于 a = a + 1
```
自操作运算包括以下几种:

|操作符|描述|
|:-|:-|
|+=|左值与右值进行加法后再赋值给左值|
|-=|左值与右值进行减法后再赋值给左值|
|*=|左值与右值进行乘法后再赋值给左值|
|/=|左值与右值进行除法后再赋值给左值|
|%=|左值用右值取模后再赋值给左值|
|**=|左值与右值进行指数运算后再赋值给左值|
|\|=|左值与右值按位或后再赋值给左值|
|&=|左值与右值按位与后再赋值给左值|
|^=|左值与右值按位异或后再赋值给左值|
|<<=|左值进行左移位后再赋值给左值|
|>>=|左值进行右移位后再赋值给左值(最高位不参与移位)|
|>>>=|左值进行右移位后再赋值给左值(最高位参与移位)|
|&&=|当左值转化为布尔值为假时将右值赋值给左值|
|\|\|=|当左值转化为布尔值为真时将右值赋值给左值|
|~=|左值用右值进行匹配，将匹配结果赋值给左值|


语法描述：
```
self_operation_assignment_expression: assignment_left_value self_operation_assignment_operator expression

self_operation_assignment_operator: "+="
    | "-="
    | "*="
    | "/="
    | "%="
    | "**="
    | "|="
    | "&="
    | "^="
    | "<<="
    | ">>="
    | ">>>="
    | "&&="
    | "||="
    | "~="
```