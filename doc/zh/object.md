# 对象
## 声明
对象内部包含一组属性。每个属性由一个字符串属性名和对应的属性值组成。看下面的语句：
```
o = {a:1, b:2, c:3}
```
"{"和"}"中间定义了对象的属性，每个属性通过","进行分隔，属性定义中，":"前为属性名，":"后为属性值表达式。
这条语句声明了一个对象将其赋值给变量o，对象包含3个属性，属性名"a"对应值为1，属性名"b"对应值为2，属性名"c"对应值为3。
通过属性名，可以访问对应的属性值，如果对象没有找到对应属性名的属性，返回结果null：
```
stdout.puts("{o.a}\n") //打印1
stdout.puts("{o.b}\n") //打印2
stdout.puts("{o.c}\n") //打印3
stdout.puts("{o.d}\n") //"d"属性没有定义，"o.d"结果为null，打印空字符串
```
如果同多行声明对象，属性在不同行，可以省略","。不如下面的声明：
```
{
    a:1
    b:2
    c:3
}
//等价于{a:1,b:2,c:3}
```
如果属性名不能用标识符表示，可以直接用字符串表示属性名。比如：
```
id = 123

{
    "/": 0 //属性名为"/"
    "?a": true //属性名为"?a"
    "p{id}": 12 //属性名为"p123"
}
```
在程序中，还可以动态计算属性名，比如：
```
pn = "prop"
o = {
    [pn+"1"]: 1 //等价于 prop1: 1
    [pn+"2"]: 2 //等价于 prop2: 2
    [pn+"3"]: 3 //等价于 prop3: 3
}
```
"["和"]"中为一个表达式，属性名是这个表达式的计算结果。
## 属性展开
我们可以通过属性展开将一个对象的所有属性添加到另一个对象中。看下面的代码：
```
o1 = {a:1, b:2}
o2 = {...o1, c:3} //o2 = {a:1, b:2, c:3}
```
## 属性追加
如果一个对象中已定义一个属性，向这个属性赋值，可以修改这个属性值。如：
```
o = {a:1, b:2} //定义对象，设定属性“a”和“b"
o.b = o.a //o的“b"属性值变为1
o.a = -1 //o的"a"属性值变为-1
```
如果赋值的属性在对象中没有定义，则自动向对象中追加这个属性，并设定其属性值。如:
```
o = {} //对象o中没有定义属性
o.a = 1 //对象o中增加属性"a"，其值为1
o.a = 2 //将对象o的属性"a"值变为2
```
如果要追加多个属性，可以通过以下方式：
```
o = {a:1, b:2}
o.{
    c:3 //追加属性"c"，值为3
    d:4 //追加属性"d"，值为4
}
```

语法描述：
```
object_literal: "{" object_properties? "}"

object_properties: object_properties "," object_property
    | object_property

object_property: property_name ":" expression
    | "..." expression

property_name: identifier
    | string_literal
    | number_literal
    | "[" expression "]"

object_append_properties: expression "." "{" object_properties? "}"
```